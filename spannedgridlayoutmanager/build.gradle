apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'maven-publish'

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaAndroidTask) {
    delete "$rootDir/docs"

    outputFormat = 'html'
    outputDirectory = "$rootDir/docs"

    externalDocumentationLink {
        url = new URL("https://developer.android.com/reference/")
    }

    includeNonPublic = false

    // Always recreate documentation
    outputs.upToDateWhen { return false }
}

afterEvaluate {
    if (project.hasProperty("javadocJar")) {
        tasks.javadocJar.dependsOn dokkaJavadoc
    }
}

ext {
    bintrayRepo = 'maven'
    bintrayName = 'spannedgridlayoutmanager'

    publishedGroupId = 'com.arasthel'
    libraryName = 'SpannedGridLayoutManager'
    artifact = 'spannedgridlayoutmanager'

    libraryDescription = 'A layout manager that will resize and reorder views based on a provided SpanSize.'

    siteUrl = 'https://github.com/Arasthel/SpannedGridLayoutManager'
    gitUrl = 'https://github.com/Arasthel/SpannedGridLayoutManager.git'

    libraryVersion = '3.0.3'

    developerId = 'Arasthel'
    developerName = 'Jorge Mart√≠n Epsinosa'
    developerEmail = 'jorgemartinespinosa@gmail.com'

    licenseName = 'MIT'
    licenseUrl = 'https://opensource.org/licenses/MIT'
    allLicenses = ["MIT"]
}

android {
    compileSdkVersion 29
    buildToolsVersion buildtools_version

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 29
        versionCode 1
        versionName libraryVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

tasks.withType(Javadoc) {
    excludes = ['**/*.kt']
}

dependencies {
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    publications {
        bar(MavenPublication) {
            groupId 'com.arasthel'
            artifactId 'spannedgridlayoutmanager'
            version libraryVersion
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/spannedgridlayoutmanager-release.aar")
        }
    }
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}

// Bintray Upload
apply from: '../gradle/tools/bintrayv1.gradle'
apply from: '../gradle/tools/installv1.gradle'